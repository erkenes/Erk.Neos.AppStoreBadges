prototype(Erk.Neos.AppStoreBadges:Component.Atom.AppleAppStore) < prototype(Neos.Fusion:Component) {
    // API
    inBackend = false
    appStoreUri = ''
    language = 'en'
    style = 'black'
    storeType = 'io2s'

    // Renderer
    renderer = Neos.Fusion:Tag {
        tagName = 'a'
        tagName.@process.setSpan = 'span'
        tagName.@process.setSpan.@if.isBackend = ${props.inBackend}
        attributes {
            href = ${props.appStoreUri}
            target = '_blank'

            class = 'appstore-badge-link'
            class.@process.addIosStore = ${value + ' apple-app-store-badge-link'}
            class.@process.addIosStore.@if.isIos = ${props.storeType == 'ios'}
            class.@process.addMacStore = ${value + ' apple-mac-store-badge-link'}
            class.@process.addMacStore.@if.isMac = ${props.storeType == 'mac'}
            class.@process.addTvStore = ${value + ' apple-tv-store-badge-link'}
            class.@process.addTvStore.@if.isTv = ${props.storeType == 'tv'}

            class.@process.addLightStyle = ${value + ' apple-store-badge-white'}
            class.@process.addLightStyle.@if.isLight = ${props.style == 'white'}
        }
        content = Neos.Fusion:Tag {
            tagName = 'img'
            attributes {
                src = 'https://tools.applemediaservices.com/api/badges/download-on-the-app-store/'
                src.@process.setMacStore = 'https://tools.applemediaservices.com/api/badges/download-on-the-mac-app-store/'
                src.@process.setMacStore.@if.isMac = ${props.storeType == 'mac'}
                src.@process.setTvStore = 'https://tools.applemediaservices.com/api/badges/download-on-apple-tv/'
                src.@process.setTvStore.@if.isTv = ${props.storeType == 'tv'}
                src.@process.addStyle = ${value + props.style + '/'}
                src.@process.addLanguage = ${value + props.language + '-' + props.language}
                src.@process.addSize = ${value + '?size=250x83'}
                alt = ${Translation.translate('label', null, [], 'Component/Atom/AppleAppStore', 'Erk.Neos.AppStoreBadges')}

                class = ''
                class.@process.addIosStore = ${value + ' apple-app-store-badge'}
                class.@process.addIosStore.@if.isIos = ${props.storeType == 'ios'}
                class.@process.addMacStore = ${value + ' apple-mac-store-badge'}
                class.@process.addMacStore.@if.isMac = ${props.storeType == 'mac'}
                class.@process.addTvStore = ${value + ' apple-tv-store-badge'}
                class.@process.addTvStore.@if.isTv = ${props.storeType == 'tv'}
            }
        }
    }
}
